grammar org.xtext.example.mdsd.AndroidGenerator with org.eclipse.xtext.common.Terminals
generate androidGenerator "http://www.xtext.org/example/mdsd/AndroidGenerator"
 
AndroidAppProject:
    (applications += Application)*
;
 
terminal PACKAGE_NAME:
    '"' ( ID ('.' ID)* ) '"'
;
Application:
    'App' name=ID attributes += ApplicationAttribute* 
;
 
ApplicationAttribute:
        ApplicationVersion			|
        ApplicationPermissionList   |
        ApplicationElementList      
;

ApplicationVersion:
	{ApplicationVersion} BEGIN 'Android-SDK' ':' result=Number (targetsdk=TargetSDK)? END
;

TargetSDK:
	{TargetSDK} 'Target-SDK' ':' result=Number (minsdk=MinSDK)?
;

MinSDK:
	{MinSDK} 'Minimum-SDK' ':' result=Number
;
 
ApplicationPermissionList:
    'permissions' permissions+=Permission (',' permissions += Permission)*
;
 
Permission:
    name = PACKAGE_NAME // android permissions are in packagename fortmat (e.g. "android.permission.INTERNET")
;
 
ApplicationElementList:
    {ApplicationElementList} 'elements' BEGIN elements += ApplicationElement* END
;

ApplicationElement:
	Fragment | Model
;

Model:
	'Model' name = ID
;
 
Fragment:
	{Fragment} 'Fragment' name=ID (activityAttributes+=ActivityLayoutAttributes )? 
;

ActivityLayoutAttributes:
	BEGIN layoutElements += LayoutElement (',' layoutElements += LayoutElement)* END
;

LayoutElement:
	Button 					| 
	Dataholders				|
	TypeMap					|
	TypeSetting
;

Dataholders:
	EditText 				|
	Spinner					|
	TextView
;

TypeMap:
	{TypeMap} 'Map' (activitytypeattribute = ActivityTypeAttributes)?
;

TypeSetting:
	{TypeSetting} 'Settings'
;

ActivityTypeAttributes:
    filter=FilterQuery
;
 
EditText:
    'EditText' name=ID
;

Spinner:
	'Spinner' name=ID ('Content' ':' spinnercon += SpinnerCon (',' spinnercon += SpinnerCon)*)?
;

SpinnerCon:
	text = STRING
;

TextView:
	'Textview' name=ID 'content' ':' text = STRING
;

Button:
    {Button}'Button' name=ID BEGIN (actions += ButtonActions (',' actions += ButtonActions)*)? END
;

ButtonActions:
	Toast | Bundle
;

Toast:
	'Toast' text = STRING
;

Bundle:
	'Bundle' bundleRecipient = [Fragment] (holder = Holder)?
;

Holder:
	'Holder' hol += [Dataholders] (',' hol += [Dataholders])*
;

FilterQuery:
    {FilterQuery} 'FilterQuery' name=ID filterAttributes=FilterAttributes
;
 
FilterAttributes:
    distance=Distance
;
 
Distance:
    'Distance' ':' unit=Unit operator=Operator results=Number
;
 
Number:
    value=INT
;
 
Operator:
    {Less} '>' | {More} '<' | {Equal} '='
;
 
Unit:
    {Meters} 'm' | {Imperial} 'f'
;

terminal BEGIN:
	'synthetic:BEGIN'
;
terminal END:
	'synthetic:END'
;
