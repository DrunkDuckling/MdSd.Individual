grammar org.xtext.example.mdsd.AndroidGenerator with org.eclipse.xtext.common.Terminals
generate androidGenerator "http://www.xtext.org/example/mdsd/AndroidGenerator"
 
AndroidAppProject:
    (applications += Application)*
;

Application:
    'App' ':' name=ID attributes += ApplicationAttribute* 
;
 
ApplicationAttribute:
        ApplicationVersion			|
        ApplicationPermissionList   |
        ApplicationElementList      
;

ApplicationVersion:
	{ApplicationVersion} BEGIN 'Android-SDK' ':' result=Number (targetsdk=TargetSDK)? END
;

TargetSDK:
	{TargetSDK} 'Target-SDK' ':' result=Number (minsdk=MinSDK)?
;

MinSDK:
	{MinSDK} 'Min-SDK' ':' result=Number
;
 
ApplicationPermissionList:
    'Permissions' ':' BEGIN permissions+=Permission (',' permissions += Permission)* END
;
 
Permission:
    name = PACKAGE_NAME // android permissions are in packagename fortmat (e.g. "android.permission.INTERNET")
;
 
ApplicationElementList:
    {ApplicationElementList} 'Elements' ':' BEGIN elements += ApplicationElement* END
;

ApplicationElement:
	Fragment | Model
;

Model:
	'Model' ':' name = ID
;
 
Fragment:
	'Fragment' ':' name=ID (fragmentAttributes+=FragmentLayoutAttributes)? 
;

FragmentLayoutAttributes:
	BEGIN layoutElements += LayoutElement (',' layoutElements += LayoutElement)* END
;

LayoutElement:
	Button 					| 
	Dataholders				|
	TypeMap					|
	TypeSetting
;

Dataholders:
	EditText 				|
	Spinner					|
	TextView
;

TypeMap:
	{TypeMap} 'Map' ':' (BEGIN fragmenttypeattribute = FragmentTypeAttributes END)? 
;

TypeSetting:
	{TypeSetting} 'Settings'
;

FragmentTypeAttributes:
    filter=FilterQuery
;
 
EditText:
    'EditText' ':' name=ID
;

Spinner:
	'Spinner' ':' name=ID ('Content' ':' spinnercon += SpinnerCon (',' spinnercon += SpinnerCon)*)?
;

SpinnerCon:
	text = STRING
;

TextView:
	'Textview' ':' name=ID 'Content' ':' text = STRING
;

Button:
    {Button}'Button' ':' name=ID BEGIN (actions += ButtonActions (',' actions += ButtonActions)*)? END
;

ButtonActions:
	Toast | Bundle
;

Toast:
	'Toast' ':' text = STRING
;

Bundle:
	'Bundle' ':' bundleRecipient = [Fragment] (holder = Holder)?
;

Holder:
	'Holder' ':' hol += [Dataholders] (',' hol += [Dataholders])*
;

FilterQuery:
    {FilterQuery} 'FilterQuery' ':' name=ID filterAttributes=FilterAttributes
;
 
FilterAttributes:
    distance=Distance
;
 
Distance:
    'Dist' ':' unit=Unit operator=Operator results=Number
;
 
Number:
    value=INT
;
 
Operator:
    {Less} '>' | {More} '<' | {Equal} '='
;
 
Unit:
    {Meters} 'm' | {Imperial} 'f'
;

terminal BEGIN:
	'synthetic:BEGIN'
;
terminal END:
	'synthetic:END'
;

terminal PACKAGE_NAME:
    '"' ( ID ('.' ID)* ) '"'
;
